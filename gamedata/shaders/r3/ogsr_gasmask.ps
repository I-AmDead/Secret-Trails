/*
    Gasmask overlay shader

    Author: LVutner
    Credits: ZHC (visor reflection shader)
*/

#include "common.h"
#include "ogsr_gasmask_common.h"
#include "visor.h"

float4 main(p_screen I) : SV_Target
{
    // Sample gasmask texture
    float4 gasmask_tex;
    float4 smear_tex;

    I.tc0 = applyInertia(I.tc0);

    if (m_actor_params.y > 0.9)
        gasmask_tex = s_mask_nm_1.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.8)
        gasmask_tex = s_mask_nm_2.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.7)
        gasmask_tex = s_mask_nm_3.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.6)
        gasmask_tex = s_mask_nm_4.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.5)
        gasmask_tex = s_mask_nm_5.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.4)
        gasmask_tex = s_mask_nm_6.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.3)
        gasmask_tex = s_mask_nm_7.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.2)
        gasmask_tex = s_mask_nm_8.Sample(smp_nofilter, I.tc0);
    else if (m_actor_params.y > 0.1)
        gasmask_tex = s_mask_nm_9.Sample(smp_nofilter, I.tc0);
    else
        gasmask_tex = s_mask_nm_10.Sample(smp_nofilter, I.tc0);

    if (raindrop_restart.y > 3)
        smear_tex = s_mask_smear_4.Sample(smp_nofilter, I.tc0);
    else if (raindrop_restart.y > 2)
        smear_tex = s_mask_smear_3.Sample(smp_nofilter, I.tc0);
    else if (raindrop_restart.y > 1)
        smear_tex = s_mask_smear_2.Sample(smp_nofilter, I.tc0);
    else
        smear_tex = s_mask_smear_1.Sample(smp_nofilter, I.tc0);

    smear_tex.w = raindrop_restart.x;

    // Prepare refracted texcoord
    float2 gasmask_tc = (gasmask_tex.xy * 2.0 - 1.0) * GM_DIST_INT;
    float2 swear_tc = ((smear_tex.xy * 2.0 - 1.0) * GM_DIST_INT / 4.0) * raindrop_restart.x;
    float2 refr_tc = I.tc0.xy + gasmask_tc + swear_tc;

    // Sample scene with refracted texcoord
    float3 image = s_image.Sample(smp_nofilter, refr_tc.xy).xyz;

    float3 blurredImage = s_blur_2.Sample(smp_nofilter, refr_tc.xy).xyz;

    float2 center = float2(0.5, 0.5);
    float distFromCenter = length(refr_tc.xy - center);
    image = lerp(image, blurredImage, (m_actor_params.y / 10) * abs(distFromCenter * 1.5));

    // Add glass reflection on top
    if (mask_control.z == 1)
        image = visor_reflection(image, I.tc0.xy);

    float gasmask_tex_alpha = gasmask_tex.w * GM_DIFF_INT;
    float smear_tex_alpha = smear_tex.w * GM_DIFF_INT / 4.0;

    // Mix gasmask cracks with image
    image += (gasmask_tex_alpha + smear_tex_alpha) * image;

    // Output
    return float4(image, 1.0);
}