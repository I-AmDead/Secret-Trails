#include "common.h"
#include "screenspace_hud_raindrops.h"

uniform float4 m_affects;

float get_noise(float2 co) { return (frac(sin(dot(co.xy, float2(12.9898, 78.233))) * 43758.5453)) * 0.5; }

struct v2p
{
    float2 tc0 : TEXCOORD0; // main tc for screen
    float3 RDrops : TEXCOORD1; // Raindrops
    float4 c0 : COLOR0; // unused shit mb vertex shader idk
    // float4 hpos : SV_Position;
};

float4 main(v2p I) : SV_Target
{
    // HUD Rain drops - SSS Update 17
    // https://www.moddb.com/mods/stalker-anomaly/addons/screen-space-shaders/
    float extra_col = 0;
    if (ssfx_hud_drops_1.y > 0)
    {
        float4 Layer0 = s_hud_rain.Sample(smp_base, (I.tc0 + float2(0, -ssfx_hud_drops_1.x * 0.01f)) * float2(0.5f, 0.75f)); // Big drops
        float4 Layer1 = s_hud_rain.Sample(smp_base, I.tc0 * float2(5.0f, 3.0f)); // Small drops [ Static ]

        float3 result = ssfx_process_drops(Layer0, 0.8f, 0.9f) + ssfx_process_drops(Layer1, 0.2f, 1.0f);
        result.xy = clamp(result.xy, -1.0f, 1.0f);
        result.xyz *= saturate(I.RDrops.y);
        result.xyz *= ssfx_hud_drops_1.y;
        I.tc0.xy = I.tc0.xy - result.xy * 0.4f;
        extra_col = saturate(Layer0.x + Layer1.x) * result.z * 0.4f;
    }

    float problems = frac(timers.z * 5 * (1 + 2 * m_affects.x)) * 2;
    I.tc0.x += (m_affects.x > 0.09 && I.tc0.y > problems - 0.01 && I.tc0.y < problems) ? sin((I.tc0.y - problems) * 5 * m_affects.y) : 0;

    problems = cos((frac(timers.z * 2) - 0.5) * 3.1416) * 2 - 0.8;
    float AMPL = 0.13;
    I.tc0.x -= (m_affects.x > 0.15 && I.tc0.y > problems - AMPL && I.tc0.y < problems + AMPL) ?
        (cos(4.71 * (I.tc0.y - problems) / AMPL) * sin(frac(timers.z) * 6.2831 * 90) * 0.02 * (AMPL - abs(I.tc0.y - problems)) / AMPL) : 0;

    I.tc0.x += (m_affects.x > 0.38) ? (m_affects.y - 0.5) * 0.04 : 0;

    float4 t_vp2 = s_base.Sample(smp_base, I.tc0);

    float noise = get_noise(I.tc0 * timers.z) * m_affects.x * m_affects.x * 15;
    t_vp2.r += noise;
    t_vp2.g += noise;
    t_vp2.b += noise;
    t_vp2.a += noise;

    t_vp2.rgba = (m_affects.x > 0.41) ? 0 : t_vp2.rgba;

    return t_vp2 + extra_col;
}