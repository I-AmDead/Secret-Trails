#include "common\common.h"

// #define USE_HASHED_ALPHA_TEST
#include "common\sload.h"

#include "common\common_brdf.h"

#include "common\screenspace\check_screenspace.h"

// SSS Settings
#ifdef SSFX_FLORAFIX
#include "common\screenspace\settings_screenspace_FLORA.h"
#endif

static float DITHER_THRESHOLDS[16] = {1.0 / 17.0, 9.0 / 17.0,  3.0 / 17.0, 11.0 / 17.0, 13.0 / 17.0, 5.0 / 17.0, 15.0 / 17.0, 7.0 / 17.0,
                                      4.0 / 17.0, 12.0 / 17.0, 2.0 / 17.0, 10.0 / 17.0, 16.0 / 17.0, 8.0 / 17.0, 14.0 / 17.0, 6.0 / 17.0};

struct PSInput
{
    float2 tcdh : TEXCOORD0;
    float4 position : TEXCOORD1;
    float4 N : TEXCOORD2;
    float4 hpos_curr : TEXCOORD8;
    float4 hpos_old : TEXCOORD9;
    float4 hpos : SV_Position;
};

f_deffer main(PSInput I)
{
    float4 D = tbase(I.tcdh);

    hashed_alpha_test(I.tcdh.xy, D.a);

    float ms = xmaterial;
    float gloss = def_gloss;

    // Dither
    float4 Postc = mul(m_P, float4(I.position.xyz, 1));
    float2 tc = (Postc.xy / Postc.w) * float2(0.5f, -0.5f) + 0.5f;

    float2 dither_uv = tc * screen_res.xy; // Aspect ratio
    uint dither_idx = (uint(dither_uv.x) % 4) * 4 + uint(dither_uv.y) % 4;

    clip(I.N.w - DITHER_THRESHOLDS[dither_idx]);

#ifdef SSFX_FLORAFIX
    // Material value ( MAT_FLORA )
    ms = MAT_FLORA;

    // Fake gloss
    gloss = lerp(ssfx_florafixes_1.x, ssfx_florafixes_1.y, rain_params.y);
#endif

#ifdef USE_LM_HEMI
    float h = s_hemi.Sample(smp_rtlinear, I.lmh).a;
#else
    float h = I.position.w;
#endif

    float4 Ne = float4(normalize((float3)I.N.xyz), h);

    f_deffer O = pack_gbuffer(Ne, float4(I.position.xyz + Ne.xyz * def_virtualh / 2.h, ms), float4(D.rgb, gloss));

    O.Velocity = get_motion_vector(I.hpos_curr, I.hpos_old);

    return O;
}
